//file:noinspection GroovyAssignabilityCheck
//file:noinspection GroovyAccessibility
//file:noinspection DependencyNotationArgument
plugins {
	id "maven-publish"
	alias libs.plugins.loom
	alias libs.plugins.githubRelease
	alias libs.plugins.minotaur
}

allprojects {
	apply plugin: "maven-publish"
	apply plugin: "fabric-loom"

	version = "$baseVersion+$branch"
	archivesBaseName = project == rootProject ? slug : "${slug}-${project.name}"

	repositories {
		mavenCentral()
		maven { url "https://maven.fabricmc.net/" }

		maven { url "https://repo.sleeping.town/" }
		maven { url "https://maven.nucleoid.xyz/" }
		maven { url "https://maven.wispforest.io" }
		maven { url "https://oss.sonatype.org/content/repositories/snapshots" }
		maven { url "https://maven.ladysnake.org/releases" }
		maven { url "https://maven.cafeteria.dev" }
		maven { url "https://maven.jamieswhiteshirt.com/libs-release" }
		maven { url "https://maven.shedaniel.me/" }
		maven { url "https://maven.terraformersmc.com/releases" }
		maven { url "https://masa.dy.fi/maven" }
		maven { url "https://maven.quiltmc.org/repository/release" }

		maven { url "https://api.modrinth.com/maven" }
		maven { url "https://jitpack.io" }
	}

	dependencies {
		minecraft libs.mc
		mappings variantOf(libs.yarn) { classifier "v2" }
		modImplementation libs.fl

		modImplementation('me.lucko:fabric-permissions-api:0.3.3')
	}

	processResources {
		final Map<String, String> meta = [
			version              : version,
			modId                : modId,
			modName              : modName,
			modDescription       : modDescription,
			coreId               : coreId,
			coreName             : coreName,
			coreDescription      : coreDescription,
			clientId             : clientId,
			clientName           : clientName,
			clientDescription    : clientDescription,
			cardinalId           : cardinalId,
			cardinalName         : cardinalName,
			cardinalDescription  : cardinalDescription,
			uiId                 : uiId,
			uiName               : uiName,
			uiDescription        : uiDescription,
			compatId             : compatId,
			compatName           : compatName,
			compatDescription    : compatDescription,
			compatUiId           : compatUiId,
			compatUiName         : compatUiName,
			compatUiDescription  : compatUiDescription,
			cardinalUiId         : cardinalUiId,
			cardinalUiName       : cardinalUiName,
			cardinalUiDescription: cardinalUiDescription,
			homepage             : "https://modrinth.com/mod/${slug}",
			issues               : "https://github.com/${user}/${slug}/issues",
			sources              : "https://github.com/${user}/${slug}",
			license              : license,
			authors              : authors.split(", ").join("\",\n    \""),
			contributors         : contributors.split(", ").join("\",\n    \""),
			members              : "${authors}. Contributions by ${contributors}",
			mc                   : compatibleVersions.split(", ")[0],
			fl                   : libs.versions.fl.get(),
			kaleidoConfig        : libs.versions.kaleidoConfig.get(),
			fapi                 : libs.versions.fapi.get(),
			serverTranslationsApi: libs.versions.serverTranslationsApi.get(),
			owo                  : libs.versions.owo.get(),
			cardinal             : libs.versions.cardinal.get(),
			styledNicknames      : libs.versions.styledNicknames.get(),
			fabrictailor         : libs.versions.fabrictailor.get(),
			fabrication          : libs.versions.fabrication.get(),
		]
		inputs.properties(meta)
		filesMatching("*.mod.json") { expand(meta) }
		filesMatching("META-INF/*mods.toml") { expand(meta) }

		if (project != rootProject) {
			from("$rootDir/src/main/resources/assets/${modId}/icon.png") {
				rename { "assets/${modId}/icon.png" }
			}
		}
		from("src/main/resources/assets/${modId}/lang/en_us.json") {
			into { "data/${modId}_assets/lang" } // loading from the same namespace causes a weird collision in some versions of STAPI
		}
	}

	tasks.withType(JavaCompile).configureEach {
		it.options.encoding = "UTF-8"
		it.options.release = 21
	}

	java {
		sourceCompatibility = JavaVersion.VERSION_21
		targetCompatibility = JavaVersion.VERSION_21
		withSourcesJar()
		withJavadocJar()
	}

	tasks.withType(Jar).configureEach {
		from("LICENSE") {
			rename { "${it}_${archivesBaseName}" }
		}
	}

	publishing {
		repositories {
			mavenLocal()
			maven {
				name "SleepingTown"
				url "https://repo-api.sleeping.town/"
				credentials(PasswordCredentials)
			}
		}

		publications {
			mavenJava(MavenPublication) {
				from components.java
				artifactId "${rootProject.slug}-${project.name}"
				version "${rootProject.baseVersion}+${rootProject.branch}"
			}
		}
	}
}

dependencies {
	final def addMod = { final String path ->
		include project(path)
		localRuntime(project(path: path, configuration: "namedElements")) { exclude(module: "fabric-loader") }
	}

	addMod(":core")
	addMod(":client")
	addMod(":ui")
	addMod(":cardinal")
	addMod(":cardinal-ui")
	addMod(":compat")
	addMod(":compat-ui")

	modLocalRuntime(libs.fapi) { exclude(module: "fabric-loader") }
	modLocalRuntime(libs.kaleidoConfig) { exclude(module: "fabric-loader") }
	modLocalRuntime(libs.serverTranslationsApi) { exclude(module: "fabric-loader") }
}

githubRelease {
	token = "$System.env.GITHUB_TOKEN"
	owner = project.user
	repo = slug
	tagName = "v${baseVersion}+${tagBranch}"
	targetCommitish = tagBranch
	releaseAssets = remapJar
	releaseName = "v${baseVersion}"
	allowUploadToExisting = true
	generateReleaseNotes = true
	body = !rootProject.file("CHANGELOG.md").exists() ? "" : rootProject.file("CHANGELOG.md").text
}

modrinth {
	token = "$System.env.MODRINTH_TOKEN"
	projectId = slug
	versionNumber = project.version
	uploadFile = remapJar
	gameVersions = compatibleVersions.split(", ").toList()
	loaders = compatibleLoaders.split(", ").toList()
	changelog = !rootProject.file("CHANGELOG.md").exists() ? "" : rootProject.file("CHANGELOG.md").text + "\n\nChangelog: https://github.com/${user}/${slug}/releases/tag/v${baseVersion}+${tagBranch}"
	syncBodyFrom = rootProject.file("README.md").text
	dependencies {
		required.version libs.fapi.get().getName(), libs.versions.fapi.get()
		optional.version libs.owo.get().getName(), libs.versions.owo.get()
		optional.version libs.styledNicknames.get().getName(), libs.versions.styledNicknames.get()
		optional.version libs.fabrictailor.get().getName(), libs.versions.fabrictailor.get()
	}
}

tasks.runClient {
	dependsOn(subprojects*.tasks*.jar)
}

tasks.runServer {
	dependsOn(subprojects*.tasks*.jar)
}

tasks.register("fullRelease") {
	group "publishing"
	dependsOn "githubRelease"
	dependsOn "modrinth"
	dependsOn "publish"
}
